//file:noinspection SpellCheckingInspection
plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.compose'
    // id 'jacoco'
}

android {
    signingConfigs {
        release {
            storeFile file("../../gocards.jks")
            storePassword project.property('RELEASE_STORE_PASSWORD')
            keyAlias project.property('RELEASE_KEY_ALIAS')
            keyPassword project.property('RELEASE_KEY_PASSWORD')
        }
    }
    namespace = 'pl.gocards'
    compileSdk 36

    defaultConfig {
        applicationId "pl.gocards"
        testApplicationId "pl.gocards.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode 35
        versionName "1.0.34"
        vectorDrawables {
            useSupportLibrary = true
        }
        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "@string/app_name_release"
            signingConfig = signingConfigs.release
            ext.enableCrashlytics = true
            firebaseCrashlytics {
                mappingFileUploadEnabled = true
            }
        }
        debug {
            minifyEnabled false
            shrinkResources = false
            // proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "@string/app_name_debug"
            versionNameSuffix '-DEV'
            ext.enableCrashlytics = true
            firebaseCrashlytics {
                // mappingFileUploadEnabled true
            }
            // testCoverageEnabled = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    dynamicFeatures = [
            ':filesync',
            ':filesync_pro',
            ':filesync_excel',
            ':filesync_csv',
            ':ai_chat'
    ]
    buildFeatures {
        buildConfig = true
        compose = true
        viewBinding = true
        resValues = true
    }
    packagingOptions {
        resources {
            excludes += 'META-INF/{AL2.0,LGPL2.1}'
            excludes += 'META-INF/DEPENDENCIES'
            excludes += 'Log4j-charsets.properties'
            merges   += 'META-INF/services/org.xml.sax.driver'
        }
    }
}

dependencies {
    implementation project(':room')
    implementation project(":filesync_sheet")

    // Compose
    implementation platform("androidx.compose:compose-bom:$compose_bom")
    implementation "androidx.compose.ui:ui"
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.material:material-icons-extended-android"
    implementation 'androidx.activity:activity-compose:1.12.4'
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.1"

    // MDC-Android
    implementation "com.google.android.material:material:$material"
    implementation 'androidx.recyclerview:recyclerview:1.4.0'

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Android
    implementation "androidx.work:work-runtime:$work_version"

    // Play Core for dynamic features
    implementation "com.google.android.play:feature-delivery:2.1.0"

    // Apache POI
    implementation project(":android-apache-poi-5-with-dependencies")
    // implementation "pl.gocards:android-apache-poi-5-with-dependencies:$android_apache_poi_version"

    // Apache Commons - must be in base app to ensure transitive dependencies are packaged
    // Dynamic feature modules (filesync_csv) require these, but D8 won't include them
    // in split APKs unless they're declared in the base module
    implementation 'commons-io:commons-io:2.21.0'
    implementation 'org.apache.commons:commons-text:1.15.0'

    implementation platform("com.google.firebase:firebase-bom:$firebase_bom")
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-firestore' // Must be in base module for ComponentDiscovery
    implementation 'com.google.firebase:firebase-ai' // Must be in base module for ComponentDiscovery

    implementation 'com.google.firebase:firebase-appcheck'
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'
    implementation 'com.google.firebase:firebase-appcheck-debug'
    implementation 'com.google.android.play:integrity:1.6.0'

    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.firebaseui:firebase-ui-auth:9.1.1'

    implementation 'com.android.billingclient:billing-ktx:8.3.0'

    implementation 'com.google.android.play:review-ktx:2.0.2'


    androidTestImplementation project(':filesync')
    androidTestImplementation project(':filesync_pro')
    androidTestImplementation project(':filesync_excel')
    androidTestImplementation project(':filesync_csv')

    // Test rules and transitive dependencies:
    androidTestImplementation platform("androidx.compose:compose-bom:$compose_bom")
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
    // Needed for createComposeRule(), but not for createAndroidComposeRule<YourActivity>():
    debugImplementation("androidx.compose.ui:ui-test-manifest")
    androidTestImplementation("androidx.test.uiautomator:uiautomator:2.3.0")
}

// Apply custom Gradle task for pulling screenshots from device after tests
apply from: '../gradle/pullScreenshots.gradle'